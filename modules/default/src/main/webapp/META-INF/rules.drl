###############################################################################
package org.jahia.modules.rules

#list any import classes here.
import org.jahia.services.content.rules.*
import org.jahia.services.content.*
import javax.jcr.observation.Event
import org.slf4j.Logger

expander rules.dsl

#declare any global variables here
global User user
global Service service
global ImageService imageService
global ExtractionService extractionService
global Logger logger
global JCRStoreProvider provider
###############################################################################

rule "Tagging"
   when
       A property j:newTag has been set on a node
        - the node has the type jmix:tagged
   then
       Tag the node with the propertyValueAsString
end

rule "Image type"
#Assign jmix:image type for an image node
    salience 50
    no-loop
    when
        A new node is created
             - the mimetype matches image/.*
    then
        Log "Assign image type (jmix:image) for node " + node.getPath()
        Add the type jmix:image
end

rule "Image update"
    salience 25
#Rebuild thumbnail for an updated image and update height/width
    when
        A file content has been modified
             - the mimetype matches image/.*
    then
        > long timer = System.currentTimeMillis();
        Set the property j:width of the node with the width of the image
        Set the property j:height of the node with the height of the image
        Create an image "thumbnail" of size 150
        Create an image "thumbnail2" of size 350
        Dispose image
        Log "Image " + node.getPath() + " updated in " + (System.currentTimeMillis() - timer) + " ms"
end

